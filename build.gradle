plugins {
    id 'java'
    id "maven-publish"
}

group 'top.openyuan'
version '1.0.0'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes("Automatic-Module-Name": "top.openyuan.sdt.")
        attributes("Implementation-Version": "1.0.0")
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
    options {
        encoding "UTF-8"
    }
}

dependencies {
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.25'
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation 'org.slf4j:slf4j-api:2.0.5'

    testImplementation "junit:junit:4.13.2"
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"
    testImplementation "org.awaitility:awaitility:4.2.0"

    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.5'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = group
            artifactId = 'SDT'
            version = project.version

            pom {
                name = 'SDT'
                description = 'Secure Data Transport'
                url = 'https://github.com/yourusername/your-repo'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'Central'
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
        }
    }
}
